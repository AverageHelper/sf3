# About SF3
SF3 (Simple File Format Family) is a family of file format specifications. These file formats all follow a similar scheme and the same principles.

# Principles
SF3 formats follow these principles:

- **No versioning**
  These formats explicitly do not include any versioning at all. The way they are described in this document is final and will not change. This means the formats are eternally forwards and backwards compatible.
- **No extensibility**
  There are no vendor extensibility blocks or other parts that could be added by third parties. This ensures that a consumer of these formats will always be able to read the full file and know what every single bit in it means.
- **No optional blocks**
  There are no optional blocks or parts in the formats that could be omitted. This means there is no conditional parsing needed and the structure of the files is always clear.
- **Only raw data**
  The data is not compressed, encrypted, or otherwise transformed. Data is always raw. If encryption or compression is desired, the entire file can instead be wrapped in a compression or encryption stream (gzip, lzma, etc).
- **Always little-endian**
  The formats are always little-endian wherever byte order matters. This is compatible with the vast majority of processors and software today and means no byte rearrangement is necessary when loading to memory.
- **Similar layout**
  Each format in the family follows a very similar format of identifier, header, and payload. This ensures that the files remain easy to parse, understand, and debug.

# Nomenclature
The following clarifies how to interpret certain words in regards to this standard:

- **file** -- A bounded sequence of octets that should be interpreted as having a structure of one of the formats outlined in this standard.
- **implementation** -- A program that is capabale of interpreting files in accordance with this specification.
- **must** -- If a file should violate this requirement, it is invalid and must be rejected by the implementation.
- **must not** -- If a file should meet this requirement, it is invalid and must be rejected by the implementation.
- **should** -- It is heavily recommended to follow this requirement, however implementations must be able to handle the case where this requirement is not met.
- **should not** -- It is heavily recommended not to follow this requirement, however implementations must be able to handle cases where it is met.
- **may** -- The behaviour is optional, however implementations must be able to handle it.

# Specification Description
The format specifications in this document use a BNF-style abstract syntax language. The language is defined here:

:: BNF
Format       ::= Definition+
Definition   ::= Rule Identifier? Description? '\n'
Identifier   ::= '::=' Sequence
Description  ::= '---' text
Sequence     ::= Composition (' ' Composition)*
Composition  ::= (Rule | OctetArray | Octet | Type | Switch | BitRange | '(' Sequence ')') (OneOrMore | AnyNumber | ExactNumber)?
OctetArray   ::= '[' octet (' ' octet)* ']'
Type         ::= ('int' | 'uint') IntBittage | 'float' FloatBittage | 'string' ExactNumber
IntBittage   ::= '8' | '16' | '24' | '32' | '64'
FloatBittage ::= '16' | '32' | '64'
Rule         ::= name
OneOrMore    ::= '+'
AnyNumber    ::= '*'
ExactNumber  ::= '{' number '}'
Switch       ::= '<' Rule SwitchCase ('|' SwitchCase)* '>'
SwitchCase   ::= octet+ ':' Sequence
BitRange     ::= Rule ':' (mask ',')? mask

name         --- The name of a rule as a sequence of non-numeric ASCII characters.
octet        --- Eight bits expressed as two hexadecimal digits.
text         --- Human-readable textual description of the contents.
number       --- A textual description of the number of occurrences. Can make a reference to other rules, in which case the rule's content designates a runtime number.
mask         --- A textual description of the number of bits to use.
::

White space unless otherwise mandated may be inserted liberally to aid readability. Each rule ultimately defines a sequence of octets that should be parsed. The ``Type``s mentioned translate to signed integers, unsigned integers, and IEEE floating point numbers of the given number of bits. ``string`` designates a UTF-8 encoded character sequence with an octet length as indicated by the required following ``ExactNumber`` rule.

A ``Switch`` acts as a runtime switching based on the value of the referenced ``Rule``. This means that when evaluated, the ``Switch`` should act as if it were the ``Sequence`` of the ``SwitchCase`` whose ``octet`` sequence matches the value of the referenced ``Rule``. If the ``Rule`` evaluates to a value that does not match any of the ``SwitchCase``s, the file is invalid. For example, ``<x 00: int8 | 01: int16>`` with ``x`` evaluating to ``00`` would match an ``int8``, and when evaluating to ``01`` would match an ``int16``. 

A ``BitRange`` extracts the bits of the specified ``Rule`` by shifting the integer to the right by the first ``mask`` number of bits, if given. It then masks the remaining integer such that only the number of bits specified in the second ``mask`` remain. For example, ``x:2,4`` with ``x`` being the binary sequence ``10101101`` would leave the bits ``1011``.

# Formats
Each format is made up of the following structure, where a valid file must begin with the ``File`` rule.

:: BNF
File       ::= Identifier Header Payload
Identifier ::= [ 81 53 46 33 00 E0 D0 0D 0A 0A ] format-id
format-id  ::= uint8 --- A single octet identifying the format.
::

The rationale for the ten octets in the identifier is as follows:

- ``81`` An octet to stop byte-peekers from determining text. The octet lies in the undefined ranges of ASCII, ISO-8859-1, Windows-1252, and SJIS.
- ``53 46 33`` ASCII sequence spelling ``SF3`` for human-readability.
- ``00`` A null octet to stop C-string utilities from trying to munch the rest of the file.
- ``E0D0`` An invalid UTF-8 octet sequence.
- ``0D0A0A`` A CRLFLF sequence to catch bad line conversion utilities.

The ``Header`` and ``Payload`` will be described by the individual formats.
The values for the ``format-id`` are interpreted as follows:

- ``00`` --- Text
- ``01`` --- Image
- ``02`` --- Audio
- ``03`` --- Model
- ``04`` --- Archive
- ``05`` --- Log
- ``06`` --- Vector Graphic

Any other value for the ``format-id`` is reserved for future formats in this spec. If an implementation only supports a subset of these formats, it must generate an error when it encounters a ``format-id`` that it does not support.

## Archive
The Archive format allows storing multiple files in one binary package. The file also includes some metadata so that the files can be stored with a relative path and mime-type, allowing both file-system extraction, and content inspection without explicit extraction.

:: BNF
Header       ::= Count MetadataSize
Payload      ::= Metadata Files
Count        ::= uint64                --- The number of entries.
MetadataSize ::= uint64                --- The octet size of the Metadata payload.
Metadata     ::= EntryOffset{Count} MetaEntry{Count}
EntryOffset  ::= uint64                --- The octet offset of the corresponding MetaEntry from the beginning of Metadata.
MetaEntry    ::= FileSize ModTime Mime Checksum Path
                                       --- A descriptor of file size, mime type, and path.
FileSize     ::= uint64                --- The size of the corresponding file in octets.
ModTime      ::= uint64                --- A unix-time timestamp denoting when the file was last modified.
Mime         ::= uint8 string{uint8}   --- The mime-type of the corresponding file.
Checksum     ::= uint32                --- A CRC32 checksum.
Path         ::= uint16 string{uint16} --- The relative path of the corresponding file.
Files        ::= FileOffset{Count+1} File{Count}
FileOffset   ::= uint64                --- The octet offset of the corresponding File from the beginning of Files.
File         ::= uint8{octetsize}      --- A binary file payload.
octetsize    --- The size of the file, which is computed from FileOffset{n+1}-FileOffset{n}.
::

The included ``MetadataSize``, ``EntryOffset``, and ``FileOffset`` fields should allow constant-time access to any content within the archive.
If no mime-type is known for a file that should be stored, the corresponding ``Mime`` should be set to ``application/octet-stream``.

Each ``EntryOffset`` in ``Metadata``, and each ``FileOffset`` in ``Files`` must be larger than the preceding entry.

### Use-Case
This format is useful if you need a way to bundle files together into a single payload, and require constant-time, typed access to individual files without having to extract, decompress, or decrypt.

## Audio
This format is for storing plain audio sample data. It includes support for all types of sample formats and channel numbers out there. Unlike other formats, it also enforces the channel data to be interleaved rather than sequential, which foregoes the need for a length field, allowing the file to be written on the fly.

:: BNF
Header     ::= Samplerate Channels format 
Payload    ::= sample{Channels}*
Samplerate ::= uint32 --- The samplerate in Hz.
Channels   ::= uint8  --- The number of audio channels.
format     ::= uint8  --- A single octet identifying the per-sample data type.
sample     ::= <format 01: int8
                     | 02: int16
                     | 04: int32
                     | 08: int64
                     | 11: uint8
                     | 12: uint16
                     | 14: uint32
                     | 18: uint64
                     | 22: float16
                     | 24: float32
                     | 28: float64>
                          ---  A single-channel value in the format indicated by format.
::

Any other value for ``format`` is invalid. 
The length of the payload is unbounded, but must always be a multiple of ``Channels*format-bits`` number of bits. For a finite file it can be figured out by subtracting the header size from the file size. If the ``format`` is ``01``, then the samples are stored according to the "A-law" non-linear scheme. Otherwise the samples are linear.

### Use-Case
This format is useful for raw audio data storage, which means it should be trivial to feed into an audio playback system with minimal overhead. Unlike the traditional uncompressed audio format, Wave, this follows a much clearer and simpler specification with sensible metadata encoding.

## Image
This format is for storing raw image data. Unlike plain data however, it includes a header that completely identifies the pixel data layout and format. The format supports 3D images as well.

:: BNF
Header        ::= Width Height Depth channels format
Payload       ::= Layer{Depth}
Layer         ::= Row{Height}
Row           ::= Color{Width}
Color         ::= channel{channel-count}
Width         ::= uint32
Height        ::= uint32
Depth         ::= uint32
format        ::= uint8      --- A single octet identifying the per-channel data type.
channels      ::= uint8      --- A single octet identifying the number and order of channels.
channel-count ::= channels:4 --- The number of channels indicated by the lower 4 bits of the channels.
channel       ::= <format 01: int8
                        | 02: int16
                        | 04: int32
                        | 08: int64
                        | 11: uint8
                        | 12: uint16
                        | 14: uint32
                        | 18: uint64
                        | 22: float16
                        | 24: float32
                        | 28: float64>
                             ---  A single-channel colour value in the format indicated by format.
::

Any other value for ``format`` is invalid. 
The values for ``channels`` are interpreted as follows:

- ``01`` --- ``R``
- ``02`` --- ``RA``
- ``03`` --- ``RGB``
- ``04`` --- ``RGBA``
- ``12`` --- ``AR``
- ``13`` --- ``BGR``
- ``14`` --- ``ABGR``
- ``24`` --- ``ARGB``
- ``34`` --- ``BGRA``

Any other value for ``channels`` is invalid.
The payload must have exactly ``Width*Height*Depth*channel-count*format-bits`` number of bits.

### Use-Case
This format is useful for storing raw bitmap data that can be directly memory-mapped and read out. This is especially convenient for GPU texture uploads with DirectX, OpenGL, Vulkan, or similar.

## Log
This format is for storing generic logging and event information.

:: BNF
Header    ::= StartTime
Payload   ::= Entry*
StartTime ::= uint64                        --- The number of seconds since the Unix epoch (1st of January 1970 UTC) at which this log begins.
Entry     ::= Size Time Severity Source Category Message
Size      ::= uint32                        --- The size of the remaining log entry in octets.
Time      ::= uint64                        --- The number of milliseconds since StartTime at which this log entry was recorded.
Severity  ::= int8                          --- The severity or importance of the log entry.
Source    ::= uint8 string{uint8}           --- An identifier of the source of the log entry.
Category  ::= uint8 string{uint8}           --- An identifier of the category the entry belongs to.
Message   ::= string{remaining}             --- A human-readable message describing the event.
remaining --- The remaining number of octets in the Entry. This can be obtained by subtracting the size of the Source, Category, Time, and Severity from the Size of the Entry.
::

The ``severity`` should be zero if the message is of neutral importance, positive for increasingly vital information, and negative for increasingly detailed information.

Both the ``Source`` and ``Category`` may consist of just a null byte each if the information is not relevant.

This file format is designed such that applications can append data directly to the file to create new log entries.

### Use-Case
This format is useful for basic logging purposes in applications that run for a longer amount of time.The binary format allows quickly skipping ahead in the file to reach interesting messages or to filter out important events.

## Model
This format is for singular triangular meshes only. It does not include a scene graph or the capability for non-triangular or non-static meshes. If animation of the model is desired, animation information can be delivered separately.

:: BNF
Header         ::= format material-type MaterialSize Count
Payload        ::= Material FaceIndices Vertices
MaterialSize   ::= uint32                  --- The octet size of the Material payload.
Count          ::= uint32                  --- The number of entries in the Faces array, must be a multiple of 3.
Material       ::= Texture{material-count} --- An array of texture maps for the model's material.
Texture        ::= uint16 string{uint16}   --- A relative file path to an image.
FaceIndices    ::= uint32{Count}           --- An array of 0-based indices into the Vertices array, every 3 of which describe a face.
Vertices       ::= vertex*
Position       ::= float32 float32 float32 --- A vertex position in model-space.
UV             ::= float32 float32         --- A texture coordinate in texture-space.
Color          ::= float32 float32 float32 --- An RGB colour triplet, each channel in [0,1].
Normal         ::= float32 float32 float32 --- A surface normal, in tangent-space.
Tangent        ::= float32 float32 float32 --- A surface tangent, in tangent-space.
format         ::= uint8                   --- A single octet identifying the per-vertex format.
material-type  ::= uint8                   --- A single octet identifying the material used.
material-count ::= material-type:4         --- The number of material textures as indicated by material-type.
vertex         ::= <format 01: Position
                        |  02: Position UV
                        |  12: Position Color
                        |  22: Position Normal
                        |  03: Position UV Normal
                        |  13: Position Color Normal
                        |  04: Position UV Normal Tangent>
                                            --- A single-vertex value in the format indicated by format.
::

The values for ``material-type`` are interpreted as follows, and describe the usage and number of ``Texture``s:

- ``00`` --- (no material)
- ``01`` --- Albedo
- ``02`` --- Albedo Normal
- ``12`` --- Albedo Emission
- ``03`` --- Albedo Normal Specular
- ``13`` --- Albedo Normal Emission
- ``23`` --- Albedo Normal Metallic
- ``04`` --- Albedo Normal Metalness Roughness
- ``14`` --- Albedo Normal Specular Emission
- ``24`` --- Albedo Normal Metallic Emission
- ``05`` --- Albedo Normal Metalness Roughness Emission
- ``15`` --- Albedo Normal Metalness Roughness Occlusion
- ``06`` --- Albedo Normal Metalness Roughness Occlusion Emission

Any other value for ``material-type`` is invalid.
The Metallic texture is a combination of Metalness, Roughness, and Occlusion in the R, G, and B channels respectively.

The included ``MaterialSize`` field should allow constant-time access to the vertex data without having to parse the ``Material`` structure, if that structure is not needed. The number of ``vertex`` entries in the ``Vertices`` should be as high as the highest index in the ``Faces`` array, and must not exceed ``Count``. Similarly, each entry in the ``Faces`` array must be lower than ``Count``.

### Use-Case
This format is useful for storing uncompressed, directly accessible 3D geometry data. It is packed in such a way that it should be trivial to upload into vertex-buffers for use with GPU rendering toolkits like DirectX, OpenGL, Vulkan, or similar. For instance, the ``format`` describes the vertex-array layout, the ``Faces`` array makes up the element-buffer, and the ``Vertices`` makes up the vertex-buffer.

## Text
The text format identifies the character encoding used, followed by the raw text payload. Essentially this means it is a regular "text file", but with a header that identifies the actual encoding used and thus liberates the need for encoding detecting kludges.

:: BNF
Header   ::= encoding
Payload  ::= text
encoding ::= uint8  --- A single octet identifying a text encoding.
text     ::= uint8* --- A sequence of octets in the encoding described by the header.
::

The values for ``encoding`` are interpreted as follows:

- ``00`` --- US-ASCII
- ``01`` --- UTF-8
- ``11`` --- UTF-16
- ``21`` --- UTF-32
- ``02`` --- ISO 8859-1
- ``12`` --- ISO 8859-2
- ``22`` --- ISO 8859-3
- ``32`` --- ISO 8859-4
- ``42`` --- ISO 8859-5
- ``52`` --- ISO 8859-6
- ``62`` --- ISO 8859-7
- ``72`` --- ISO 8859-8
- ``82`` --- ISO 8859-9
- ``92`` --- ISO 8859-10
- ``A2`` --- ISO 8859-11
- ``B2`` --- ISO 8859-12
- ``C2`` --- ISO 8859-13
- ``D2`` --- ISO 8859-14
- ``E2`` --- ISO 8859-15
- ``F2`` --- ISO 8859-16
- ``03`` --- Windows-874
- ``13`` --- Windows-1250
- ``23`` --- Windows-1251
- ``33`` --- Windows-1252
- ``43`` --- Windows-1253
- ``53`` --- Windows-1254
- ``63`` --- Windows-1255
- ``73`` --- Windows-1256
- ``04`` --- EUC-CN
- ``14`` --- EUC-JP
- ``24`` --- EUC-KR
- ``34`` --- EUC-TW
- ``05`` --- Shift JIS
- ``15`` --- Big5
- ``25`` --- GBK

Any other value for the ``encoding`` is invalid.

### Use-Case
This format is useful for storing textual content without encoding ambiguities, making it possible to transfer text without having to pay the overhead that certain encodings can impose.

## Vector Graphic
This format offers a relatively simple but capable vector graphic format for scalable images.

:: BNF
Header       ::= Width Height Count
Payload      ::= Instruction{Count}
Width        ::= uint32                          --- The width of the visible canvas in units.
Height       ::= uint32                          --- The height of the visible canvas in units.
Count        ::= uint32                          --- The number of instructions to appear.
Instruction  ::= Element | Transform
Element      ::= Line | Rectangle | Circle | Polygon | Curve
Line         ::= [ 01 ] Color Thickness ShapeOutline
                                                 --- A sequence of connected line segments.
Rectangle    ::= [ 02 ] ShapeFill ShapeBounds    --- An axis-aligned rectangle.
Circle       ::= [ 03 ] ShapeFill ShapeBounds    --- An axis-aligned oval circle.
Polygon      ::= [ 04 ] ShapeFill ShapeOutline   --- A many-edged convex polygon.
Curve        ::= [ 05 ] ShapeFill ControlPoints Point{Edges}
                                                 --- A cubic Bezier curve directed by its control points.
Text         ::= [ 06 ] Color Point Font FontSize String
                                                 --- A single line of text.
Font         ::= uint16 string{uint16}           --- The name of the font family to use to render the text.
String       ::= uint16 string{uint16}           --- The string of text to be displayed
Transform    ::= Identity | Matrix
Identity     ::= [ 10 ]                          --- A reset of the current transform matrix to the 1 0 0, 0 1 0 matrix.
Matrix       ::= [ 11 ] float32{6}               --- A coordinate transform matrix to be applied to subsequent instructions.
ShapeOutline ::= Edges Point{Edges}              --- A list of edge points of a composite shape.
ShapeBounds  ::= Point Size                      --- The bounding box of a shape.
ShapeFill    ::= Color Color Thickness           --- The fill color, outline color, and outline thickness.
Point        ::= float32 float32                 --- A position in x/y.
Size         ::= float32 float32                 --- The bounding size of a shape in width/height.
Color        ::= float32 float32 float32 float32 --- An RGBA colour quadruplet in the range of [0, 1].
Edges        ::= uint16                          --- The number of points to appear in the edge list.
Thickness    ::= float32                         --- The thickness of the outline in units.
FontSize     ::= float32                         --- The size of an em in units.
::

The coordinate system should be defined with X growing to the right, Y growing upwards, and the origin being in the lower left corner of the canvas. Whenever a ``Transform`` is applied, the given matrix must be applied to all following ``Element``s until the next ``Transform``. This transformation applies to the shape as a whole, not just the ``Point``s that define it in the file.

When an element with a ``ShapeFill`` should be drawn, the fill must be drawn first, with the outline (if visible) second on top. If the shape is bounded by ``ShapeBounds``, the fill should meet the bounds, and the outline should be centred on the bounds when hitting them. If the shape is bounded by a ``ShapeOutline``, the outline must be drawn centered on the lines defined by the ``Edge``s.

For ``Curve`` and ``Polygon``, if the ``Point``s do not form a closed shape, the fill should be drawn as a closed shape by directly connecting the first and last points with a straight line.

In the case of the ``Curve``, the edges should be interpreted as follows: ``EdgePoint ControlPoint (ControlPoint EdgePoint ControlPoint)* ControlPoint EdgePoint``. As such, the ``Edges`` number for a ``Curve`` must always match ``4+3x``. Any other value is an error.

In the case of ``Polygon`` and ``Rectangle``, the ``Edges`` number must be at least ``2``. Any other value is an error.

For the ``Size``, ``Thickness``, ``FontSize``, and ``Color``, all float components must be positive and real. Infinities, NaNs, and negative numbers are an error.

For the ``Point`` and ``Matrix``, all float components must be real. Infinities and NaNs are an error. The ``Matrix`` is specified in row-major order, meaning in order the entries are m00 m01 m02 m10 m11 m12.

When rendering ``Text``, the ``Point`` specifies the location of the middle on the baseline of the first character that is rendered.

If a visualiser or editor of a vector graphic file does not have access to the font specified in a ``Font`` field, it //should// generate an error, but //may// exchange the font for a similar one. In either case, it //must// inform the user of the missing font.

### Use-Case
This format is useful for representing vector graphics in a light-weight way that should be easy to write a visualiser for. It intentionally does not specify much about text processing and instead leaves most of this up to the implementation.

# Metadata
These formats can be delivered as part of a binary stream or deposited in a file system. The following are recommendation for metadata identifiers to distinguish SF3 data without having to parse it.

## Mime-Type
The mime-types for SF3 files should be as follows, according to the format used:

- **Archive** ``application/x.sf3-archive``
- **Audio** ``audio/x.sf3``
- **Image** ``image/x.sf3``
- **Log** ``application/x.sf3-log``
- **Model** ``model/x.sf3``
- **Text** ``text/x.sf3``
- **Vector Graphic** ``image/x.sf3-vector``

If a general SF3 file should be designated, the mime-type should be ``application/x.sf3``. If/when the IANA registration for an official mime-type is approved, the ``x.`` prefix may be dropped.

## File Extension
The file extension should always end with ``.sf3``. Specifically, for the formats the following extended extensions //may// be used:

- **Archive** ``.ar.sf3``
- **Audio** ``.au.sf3``
- **Image** ``.img.sf3``
- **Log** ``.log.sf3``
- **Model** ``.mod.sf3``
- **Text** ``.txt.sf3``
- **Vector Graphic** ``.vec.sf3``
